plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.1'
}

apply plugin: 'java'
apply from: "$rootDir/gradle/remote.gradle"

version = '0.9'

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.microsoft.azure', name: 'applicationinsights-core', version: '1.0.3'
    compile group: 'com.google.code.gson', name: 'gson', version: '1.7.2'

    testCompile 'junit:junit:4.11'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.8.0'
}

// region Prepare Docker build folder

project.ext.localDockerDir = "$rootDir/build/docker"
task copyPythonScripts(type: Copy) {
    from "$rootDir/python"
    into "$localDockerDir/python"
    exclude "**/__pycache__", "**/.idea"
}

task copyDockerFiles(type: Copy) {
    from "$rootDir/docker"
    into localDockerDir
}

task copyApplicationLibs(type: Copy) {
    from project.buildDir.absolutePath + "/libs"

    // TODO: remove when the Java SDK bug will be fixed (config file parsing failed when provided as resource)
    from "$rootDir/sdk"

    into localDockerDir
}

// Installing python required libraries.
task installPythonLibraties << {
    File theInfoFile = new File("$rootDir/docker/requirements.txt")
    theInfoFile.eachLine { line ->
        exec {
            executable "python"
            args "-m", "pip", "install", line
        }
    }
}

task runPythonTests << {
    exec {
        executable "python"
        args "-m", "unittest", "discover", "-s", "$rootDir/python"
    }
}

tasks.test.dependsOn installPythonLibraties
tasks.runPythonTests.dependsOn installPythonLibraties
tasks.test.finalizedBy runPythonTests

// Creating a fat-jar instead of the original one, to be added to the image more easily.

// TODO: create a specific task to prepare docker folder.
jar.enabled = false
tasks.shadowJar.finalizedBy copyPythonScripts, copyDockerFiles, copyApplicationLibs

shadowJar {
    classifier = ''
}

// endregion Prepare Docker build folder
gradle.projectsEvaluated {
    gradle ->
        println "项目数量："+ gradle.getRootProject().getAllprojects().size()
        def projects = gradle.getRootProject().getAllprojects()
        projects.each {Project project ->
            println "项目名称："+project.name
            project.getDependencies().findAll().each {
                Object object ->
                    println object
            }
            def names = project.configurations.getNames()
            names.each {String name ->
                def configuration = project.configurations.getByName(name)
                println configuration.getName()
                configuration.each { File file ->
                    println project.name+"依赖jar包路径："+(file.getCanonicalPath())
                    copy {
                        from file
                        into "lib"
                        rename { String fileName ->
                            //防止文件重名
                            fileName = file.getParentFile().getName()+"_"+fileName
                        }
                    }
                }
            }
        }
        println "所有的project输出jar包完毕"
}
